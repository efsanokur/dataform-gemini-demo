config {
  type: "table",
    bigquery: {
    partitionBy: "report_date",
    clusterBy: ["media_source"]
  },
  database: `${dataform.projectConfig.vars.rockadsProject}`,
  name: "teli_platforms_merged_all_cost_geo",
  schema: "teli_adjust_ds",
  dependencies: ['teli_combined_single_all_cost_dm_all_geo'],
  tags: ['adjust_teli_main']
}

WITH
  geo_list AS (
  SELECT
    short_code AS geo
  FROM
    `${dataform.projectConfig.vars.rockadsProject}.mapping_tables.countries` ),
  fb_costs AS (
  SELECT
    IFNULL(ad_name,'None') AS ad,
    ad_insight_date_start AS report_date,
    IFNULL(campaign_name,'None')  AS campaign,
    IFNULL(adset_name,'None')  AS adset,
  IF
    (country='GB', 'UK', country) AS geo,
    'teli' AS app_id,
    'Facebook Ads' AS media_source,
    cast(null as string) as adjust_os_id,
    'USD'AS currency,
    SUM(total_spend) AS total_cost,
    SUM(total_outbound_clicks) AS total_outbound_clicks,
    SUM(total_impressions) AS total_impressions,
    SUM(total_clicks) AS total_clicks,
  FROM
    `${dataform.projectConfig.vars.rockadsProject}.fb_airbyte_datamarts.ads_insights_by_country_merged_2341445502805905_teli`
  GROUP BY
    ALL),

tiktok_costs AS (
  SELECT
    ad_name AS ad,
    stat_time_day AS report_date,
    campaign_name AS campaign,
    adgroup_name as adset,
  IF
    (country_code='GB', 'UK', country_code) AS geo,
    'teli' AS app_id,
    'tiktokglobal_int' AS media_source,
    cast(null as string) as adjust_os_id,
    'USD'AS currency,
    SUM(spend) AS total_cost
  FROM
    `${dataform.projectConfig.vars.rockadsProject}.tiktok_airbyte_datamarts.ads_audience_reports_by_country_merged_teli`
  GROUP BY
    ALL),

  combined_single_all_cost_dm_all_geo AS (
  SELECT
    *
  FROM
    `${dataform.projectConfig.vars.rockadsProject}.teli_adjust_ds.teli_combined_single_all_cost_dm_all_geo` ),
  distinct_combinations AS (
  SELECT
    DISTINCT report_date,
    media_source,
    adjust_os_id,
    app_id,
    campaign,
    adset,
    ad,
    currency
  FROM
    fb_costs ),
  distinct_combinations_tiktok AS (
  SELECT
    DISTINCT report_date,
    media_source,
    adjust_os_id,
    app_id,
    campaign,
    adset,
    ad,
    currency
  FROM
    tiktok_costs )

SELECT
  *,
  CAST(NULL AS NUMERIC) AS fb_total_outbound_clicks,
  CAST(NULL AS NUMERIC) AS fb_total_impressions,
  CAST(NULL AS NUMERIC) AS fb_total_clicks,
FROM
  combined_single_all_cost_dm_all_geo
WHERE
  media_source NOT IN ('Facebook Ads', 'tiktokglobal_int', 'tiktok')

UNION ALL
SELECT
  IFNULL(fbcs.ad, appsflyer_costs.ad) AS ad,
  IFNULL(fbcs.report_date, appsflyer_costs.report_date) AS report_date,
  IFNULL(fbcs.campaign, appsflyer_costs.campaign) AS campaign,
  IFNULL(fbcs.adset, appsflyer_costs.adset) AS adset,
  IFNULL(fbcs.geo, appsflyer_costs.geo) AS geo,
  IFNULL(fbcs.app_id, appsflyer_costs.app_id) AS app_id,
  IFNULL(fbcs.media_source, appsflyer_costs.media_source) AS media_source,
  IFNULL(fbcs.adjust_os_id, appsflyer_costs.adjust_os_id) AS adjust_os_id,
  IFNULL(fbcs.total_cost, 0) AS total_cost,
  IFNULL(appsflyer_costs.total_installs, 0) AS total_installs,
  IFNULL(appsflyer_costs.total_clicks, 0) AS total_clicks,
  IFNULL(appsflyer_costs.total_impressions, 0) AS total_impressions,
  IFNULL(appsflyer_costs.total_re_attributions, 0) AS total_re_attributions,
  IFNULL(appsflyer_costs.total_re_engagements, 0) AS total_re_engagements,
  IFNULL(appsflyer_costs.total_video_completions, 0) AS total_video_completions,
  IFNULL(appsflyer_costs.total_video_25p_views, 0) AS total_video_25p_views,
  IFNULL(appsflyer_costs.total_video_50p_views, 0) AS total_video_50p_views,
  IFNULL(appsflyer_costs.total_video_75p_views, 0) AS total_video_75p_views,
  IFNULL(fbcs.total_outbound_clicks, 0) AS fb_total_outbound_clicks,
  IFNULL(fbcs.total_impressions, 0) AS fb_total_impressions,
  IFNULL(fbcs.total_clicks, 0) AS fb_total_clicks,

FROM (
  SELECT
    dc.report_date,
    dc.media_source,
    dc.adjust_os_id,
    dc.app_id,
    dc.campaign,
    dc.adset,
    dc.ad,
    gl.geo,
    dc.currency,
    IFNULL(ed.total_cost, 0) AS total_cost,
    IFNULL(ed.total_outbound_clicks, 0) AS total_outbound_clicks,
    IFNULL(ed.total_impressions, 0) AS total_impressions,
    IFNULL(ed.total_clicks, 0) AS total_clicks,
  FROM
    distinct_combinations dc
  CROSS JOIN
    geo_list gl
  LEFT JOIN
    fb_costs ed
  ON
    dc.report_date = ed.report_date
    AND dc.app_id = ed.app_id
    AND dc.campaign = ed.campaign
    AND dc.adset = ed.adset
    AND dc.ad = ed.ad
    AND dc.currency = ed.currency
    AND gl.geo = ed.geo) fbcs
FULL OUTER JOIN
  (SELECT * FROM combined_single_all_cost_dm_all_geo WHERE media_source IN ('Facebook Ads') ) appsflyer_costs
ON
  fbcs.ad=IFNULL(appsflyer_costs.ad,'None')
  AND fbcs.campaign=IFNULL(appsflyer_costs.campaign,'None')
  AND fbcs.adset=IFNULL(appsflyer_costs.adset,'None')
  AND fbcs.report_date=appsflyer_costs.report_date
  AND fbcs.geo=appsflyer_costs.geo
  AND fbcs.app_id=appsflyer_costs.app_id
  AND fbcs.media_source=appsflyer_costs.media_source

# Tiktok Costs

UNION ALL
SELECT
  IFNULL(tiktoks.ad, appsflyer_costs.ad) AS ad,
  IFNULL(tiktoks.report_date, appsflyer_costs.report_date) AS report_date,
  IFNULL(tiktoks.campaign, appsflyer_costs.campaign) AS campaign,
  IFNULL(tiktoks.adset, appsflyer_costs.adset) AS adset,
  IFNULL(tiktoks.geo, appsflyer_costs.geo) AS geo,
  IFNULL(tiktoks.app_id, appsflyer_costs.app_id) AS app_id,
  IFNULL(tiktoks.media_source, appsflyer_costs.media_source) AS media_source,
  IFNULL(tiktoks.adjust_os_id, appsflyer_costs.adjust_os_id) AS adjust_os_id,
  IFNULL(tiktoks.total_cost, 0) AS total_cost,
  IFNULL(appsflyer_costs.total_installs, 0) AS total_installs,
  IFNULL(appsflyer_costs.total_clicks, 0) AS total_clicks,
  IFNULL(appsflyer_costs.total_impressions, 0) AS total_impressions,
  IFNULL(appsflyer_costs.total_re_attributions, 0) AS total_re_attributions,
  IFNULL(appsflyer_costs.total_re_engagements, 0) AS total_re_engagements,
  IFNULL(appsflyer_costs.total_video_completions, 0) AS total_video_completions,
  IFNULL(appsflyer_costs.total_video_25p_views, 0) AS total_video_25p_views,
  IFNULL(appsflyer_costs.total_video_50p_views, 0) AS total_video_50p_views,
  IFNULL(appsflyer_costs.total_video_75p_views, 0) AS total_video_75p_views,
  CAST(NULL AS NUMERIC) AS fb_total_outbound_clicks,
  CAST(NULL AS NUMERIC) AS fb_total_impressions,
  CAST(NULL AS NUMERIC) AS fb_total_clicks,

FROM (
  SELECT
    dck.report_date,
    dck.media_source,
    dck.adjust_os_id,
    dck.app_id,
    dck.campaign,
    dck.adset,
    dck.ad,
    gl.geo,
    dck.currency,
    IFNULL(ed.total_cost, 0) AS total_cost
  FROM
    distinct_combinations_tiktok dck
  CROSS JOIN
    geo_list gl
  LEFT JOIN
    tiktok_costs ed
  ON
    dck.report_date = ed.report_date
    AND dck.app_id = ed.app_id
    AND dck.campaign = ed.campaign
    AND dck.adset = ed.adset
    AND dck.ad = ed.ad
    AND dck.currency = ed.currency
    AND gl.geo = ed.geo) tiktoks
FULL OUTER JOIN
  (SELECT * FROM combined_single_all_cost_dm_all_geo WHERE media_source IN ('tiktokglobal_int', 'tiktok') ) appsflyer_costs
ON
  tiktoks.ad=appsflyer_costs.ad
  AND tiktoks.campaign=appsflyer_costs.campaign
  AND tiktoks.adset=appsflyer_costs.adset
  AND tiktoks.report_date=appsflyer_costs.report_date
  AND tiktoks.geo=appsflyer_costs.geo
  AND tiktoks.app_id=appsflyer_costs.app_id
  AND tiktoks.media_source=IF(appsflyer_costs.media_source like '%tiktok%', 'tiktokglobal_int', appsflyer_costs.media_source)